#! /bin/bash
set -ueo pipefail

git diff "${1:-HEAD^}" '*/**/CHANGELOG.md' \
  | awk -v ignore='+**Note:** Version bump only for package ' '
    BEGIN {
      # Print the header.
      print "## Packages that have NEWS.md updates";
      print "";
      print "```diff";
    }
    /^diff / {
      # New file; flush the buffer (if relevant) and stop buffering.
      if (active) printf "%s", buf;
      active = 0;
    }
    /^--- / {
      # File name "a"; start buffering.
      active = 1;
      buf = "";
    }
    active {
      # Buffer this line, but deactivate upon seeing the ignore prefix.
      buf = buf $0 "\n";
      if (substr($0, 1, length(ignore)) == ignore) active = 0;
    }
    END {
      # Flush the buffer (if relevant) and print the footer.
      if (active) printf "%s", buf;
      print "```";
    }
  '
